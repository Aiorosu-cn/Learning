[TOC]

# 时间变量

要有动画,必须需要时间变量,unity中内置时间变量有

| 名称            | 类型   | 解释                                                     |
| --------------- | ------ | -------------------------------------------------------- |
| _Time           | float4 | 4个分量的值分别是( t/20 , t , 2t , 3t),真正的时间是Y分量 |
| _SinTime        | float4 | 分量分别是( t/8 , t/4 , t/2 ,t)                          |
| _CosTime        | float4 |                                                          |
| unity_DeltaTime | float4 | 分量分别是( dt ,1/dt , smoothDt , 1/smoothDt )           |

通常,我们使用纹理动画让表面动起来,而不是直接让模型动起来

# 序列帧

序列帧,需要人工提供关键帧,然后中间的过程用插值实现

序列帧动画的精髓在于,我们需要在每个时刻计算该时刻下应该播放的关键帧的位置,并对关键帧进行纹理采样

重头戏是利用时间计算出纹理的坐标

```shader
Shader "ShaderLearn07/Image Sequence Animation" {
	Properties {
		_Color ("Color Tint", Color) = (1, 1, 1, 1)
		_MainTex ("Image Sequence", 2D) = "white" {}
		//水平分割数量
    	_HorizontalAmount ("Horizontal Amount", Float) = 8
		//竖直分割数量
    	_VerticalAmount ("Vertical Amount", Float) = 8
    	_Speed ("Speed", Range(1, 100)) = 30
	}
	SubShader {
		Tags {"Queue"="Transparent" "IgnoreProjector"="True" "RenderType"="Transparent"}
		
		Pass {
			Tags { "LightMode"="ForwardBase" }
			
			ZWrite Off
			Blend SrcAlpha OneMinusSrcAlpha
			
			CGPROGRAM
			
			#pragma vertex vert  
			#pragma fragment frag
			
			#include "UnityCG.cginc"
			
			fixed4 _Color;
			sampler2D _MainTex;
			float4 _MainTex_ST;
			float _HorizontalAmount;
			float _VerticalAmount;
			float _Speed;
			  
			struct a2v {  
			    float4 vertex : POSITION; 
			    float2 texcoord : TEXCOORD0;
			};  
			
			struct v2f {  
			    float4 pos : SV_POSITION;
			    float2 uv : TEXCOORD0;
			};  
			
			v2f vert (a2v v) {  
				v2f o;  
				o.pos = UnityObjectToClipPos(v.vertex);  
				o.uv = TRANSFORM_TEX(v.texcoord, _MainTex);  
				return o;
			}  
			
			fixed4 frag (v2f i) : SV_Target {
				//速度乘以时间得到当前时间已经偏移的距离（实际上还是对时间的缩放）
				float time = floor(_Time.y * _Speed); 
				//使用偏移距离除以行数并向下取整，得到行索引
				float row = floor(time / _HorizontalAmount);
				//使用行索引得到列索引（实际上是除法结果的余数）
				float column = time - row * _HorizontalAmount;
		
				//使用行列索引计算对应uv		
				//half2 uv = float2(i.uv.x /_HorizontalAmount, i.uv.y / _VerticalAmount);
				//uv.x += column*(1 / _HorizontalAmount);
				//uv.y -= row *(1/ _VerticalAmount);

				//上面的代码等同下面的代码			
				half2 uv = i.uv + half2(column, -row); 
				uv.x /=  _HorizontalAmount;
				uv.y /= _VerticalAmount;
				
				fixed4 c = tex2D(_MainTex, uv);
				c.rgb *= _Color;
				
				return c;
			}
			
			ENDCG
		}  
	}
	FallBack "Transparent/VertexLit"
}

```



# 滚动动画

这个需要多层纹理

- 重点在利用时间变量在水平方向对纹理坐标进行偏移,达到滚动效果
- 然后把两张纹理的纹理坐标存在同一个变量o.uv中

```shader
Shader "ShaderLearn07/Scrolling Background" {
	Properties {
		_MainTex ("Base Layer (RGB)", 2D) = "white" {}
		_DetailTex ("2nd Layer (RGB)", 2D) = "white" {}
		_ScrollX ("Base layer Scroll Speed", Float) = 1.0
		_Scroll2X ("2nd layer Scroll Speed", Float) = 1.0
		_Multiplier ("Layer Multiplier", Float) = 1
	}
	SubShader {
		Tags { "RenderType"="Opaque" "Queue"="Geometry"}
		
		Pass { 
			Tags { "LightMode"="ForwardBase" }
			
			CGPROGRAM
			
			#pragma vertex vert
			#pragma fragment frag
			
			#include "UnityCG.cginc"
			
			sampler2D _MainTex;
			sampler2D _DetailTex;
			float4 _MainTex_ST;
			float4 _DetailTex_ST;
			float _ScrollX;
			float _Scroll2X;
			float _Multiplier;
			
			struct a2v {
				float4 vertex : POSITION;
				//为了节省空间将两个纹理的uv凑成一个float4变量分别使用xy和wz表示两个坐标
				float4 texcoord : TEXCOORD0;
			};
			
			struct v2f {
				float4 pos : SV_POSITION;
				float4 uv : TEXCOORD0;
			};
			
			v2f vert (a2v v) {
				v2f o;
				o.pos = UnityObjectToClipPos(v.vertex);
				
				//frac函数 返回矢量或者标量的小数部分
				o.uv.xy = TRANSFORM_TEX(v.texcoord, _MainTex) + frac(float2(_ScrollX, 0.0) * _Time.y);
				o.uv.zw = TRANSFORM_TEX(v.texcoord, _DetailTex) + frac(float2(_Scroll2X, 0.0) * _Time.y);
				
				return o;
			}
			
			fixed4 frag (v2f i) : SV_Target {
				fixed4 firstLayer = tex2D(_MainTex, i.uv.xy);
				fixed4 secondLayer = tex2D(_DetailTex, i.uv.zw);
				//lerp函数 根据权重返回一个标量或者矢量的线性插值结果
				//此处是根据透明度插值
				fixed4 c = lerp(firstLayer, secondLayer, secondLayer.a);
				c.rgb *= _Multiplier;
				
				return c;
			}
			
			ENDCG
		}
	}
	FallBack "VertexLit"
}

```





# 顶点动画

模型没有变,变的是纹理

## 2D效果下的河流

主要在顶点着色器上进行操作

- 首先计算顶点位移 量;
- 然后利用频率控制正选函数的频率
- 为了让不同位置具有不同的位移,对上述结果加上模型空间下的位置分量,并乘以波长变量
- 对结果值乘以振幅控制幅度
- 把位移量添加到顶点位置,再进行正常的顶点变换

```shader
Shader "ShaderLearn07/Water" {
	Properties {
		_MainTex ("Main Tex", 2D) = "white" {}
		_Color ("Color Tint", Color) = (1, 1, 1, 1)
		//波的振幅大小
		_Magnitude ("Magnitude", Float) = 1
		//波动频率
 		_Frequency ("Frequency", Float) = 1
		//波长倒数，越大波越小
 		_InvWaveLength ("InvWaveLength", Float) = 10
		//纹理流动速度
 		_Speed ("Speed", Float) = 0.5
	}
	SubShader {
		// 带顶点动画的物体如果使用批处理功能可能出现问题所以要禁用批处理
		Tags {"Queue"="Transparent" "IgnoreProjector"="True" "RenderType"="Transparent" "DisableBatching"="True"}
		
		Pass {
			Tags { "LightMode"="ForwardBase" }
			
			ZWrite Off
			Blend SrcAlpha OneMinusSrcAlpha
			Cull Off
			
			CGPROGRAM  
			#pragma vertex vert 
			#pragma fragment frag
			
			#include "UnityCG.cginc" 
			
			sampler2D _MainTex;
			float4 _MainTex_ST;
			fixed4 _Color;
			float _Magnitude;
			float _Frequency;
			float _InvWaveLength;
			float _Speed;
			
			struct a2v {
				float4 vertex : POSITION;
				float4 texcoord : TEXCOORD0;
			};
			
			struct v2f {
				float4 pos : SV_POSITION;
				float2 uv : TEXCOORD0;
			};
			
			v2f vert(a2v v) {
				v2f o;
				float4 offset;
				//yzw不变
				offset.yzw = float3(0.0, 0.0, 0.0);
				//x按照角度和振幅大小偏移
				offset.x = sin(_Frequency * _Time.y  + v.vertex.z * _InvWaveLength ) * _Magnitude;
				//offset.x = sin(_Time.y) * _Magnitude;
				//根据本身的模型位置 + 偏移得到最终的位置并转换到裁剪空间
				o.pos = UnityObjectToClipPos(v.vertex + offset);
				
				o.uv = TRANSFORM_TEX(v.texcoord, _MainTex);
				o.uv +=  float2(0.0, _Time.y * _Speed);
				
				return o;
			}
			
			fixed4 frag(v2f i) : SV_Target {
				fixed4 c = tex2D(_MainTex, i.uv);
				c.rgb *= _Color.rgb;
				
				return c;
			} 
			
			ENDCG
		}
	}
	FallBack "Transparent/VertexLit"
}

```





## 公告板效果





# shadowcast效果

